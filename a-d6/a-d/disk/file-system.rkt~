#lang r6rs

;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*                                                                 *-*-
;-*-*                     File System Abstraction                     *-*-
;-*-*                                                                 *-*-
;-*-*                       Wolfgang De Meuter                        *-*-
;-*-*                   2010  Software Languages Lab                  *-*-
;-*-*                    Vrije Universiteit Brussel                   *-*-
;-*-*                                                                 *-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-

(library
 (file-system)
 (export format! new-block delete-block delete-chain!
         write-super-block! read-super-block directory
         next-bptr next-bptr! null-block? null-block filename-size
         mk ls rm whereis df)
 (import (rnrs base)
         (rnrs control)
         (rnrs io simple)
         (rnrs mutable-strings)
         (rnrs bytevectors)
         (rnrs mutable-pairs)
         (rnrs io ports)
         (prefix (a-d disk config) disk:)
         (a-d file constants))
 
 (define filename-size     10)
 (define sentinel-filename (utf8-sentinel-for filename-size))
 
 (define null-block        0)
 (define super-block-nr    null-block)

 ; organisation of the super block
 (define directory-offset 0)                             ; byte nr of the directory pointer in the superblock
 (define freelist-offset  disk:block-pointer-size)       ; byte nr of the freelist pointer in the superblock
 (define available-offset (* 2 disk:block-pointer-size)) ; byte nr of the 'blocks available' count in the superblock
 
 (define (directory supr)
   (disk:decode-fixed-natural supr directory-offset disk:block-pointer-size))
 (define (directory! supr blck)
   (disk:encode-fixed-natural! supr directory-offset disk:block-pointer-size blck))
 (define (freelist supr)
   (disk:decode-fixed-natural supr freelist-offset disk:block-pointer-size))
 (define (freelist! supr flst)
   (disk:encode-fixed-natural! supr freelist-offset  disk:block-pointer-size flst))
 (define (blocks-free supr)
   (disk:decode-fixed-natural supr available-offset disk:block-pointer-size))
 (define (blocks-free! supr free)
   (disk:encode-fixed-natural! supr available-offset disk:block-pointer-size free))
 
 (define next-offset 0) 
 (define slot-size   (+ filename-size disk:block-pointer-size))

 (define (next-bptr blck)
   (disk:decode-fixed-natural blck next-offset disk:block-pointer-size))
 (define (next-bptr! blck bptr)
   (disk:encode-fixed-natural! blck next-offset disk:block-pointer-size bptr))
 
  (define (dir-name/ptr! blck slot name bptr)
   (define offn (+ disk:block-pointer-size (* slot slot-size)) )
   (define offp (+ disk:block-pointer-size (* slot slot-size) filename-size))
   (disk:encode-string! blck offn filename-size name)
   (disk:encode-fixed-natural! blck offp disk:block-pointer-size bptr))
 (define (dir-name blck slot)
   (define offn (+ disk:block-pointer-size (* slot slot-size)))
   (disk:decode-string blck offn filename-size))
 (define (dir-ptr blck slot)
   (define offp (+ disk:block-pointer-size (* slot slot-size) filename-size))
   (disk:decode-fixed-natural blck offp disk:block-pointer-size))   
 
 (define (null-block? blck)
   (= blck null-block))
 
 (define (has-next? blck)
   (not (null-block? (next-bptr blck))))
 
 (define (empty-slot? blck slot)
   (string=? sentinel-filename (dir-name blck slot)))

 (define nr-of-dir-slots   (div (- disk:block-size disk:block-pointer-size) slot-size))
 
 (define (at-end? blck slot)
   (= slot nr-of-dir-slots))
 
 (define (read-super-block disk)
   (disk:read-block disk super-block-nr))
 
 (define (write-super-block! supr)
   (disk:write-block! supr))
 
 (define (format! disk)
   (define supr (read-super-block disk))
   (directory!   supr null-block)
   (freelist!    supr (+ super-block-nr 1)) 
   (blocks-free! supr (- disk:disk-size 1)); the superblock itself is not free
   (write-super-block! supr)
   (let high-level-format
     ((bptr (+ super-block-nr 1)))
     (let ((block (disk:read-block disk bptr)))
       (cond ((< (+ bptr 1) disk:disk-size)
              (next-bptr! block (+ bptr 1))
              (disk:write-block! block)
              (high-level-format (+ bptr 1)))
             (else
              (next-bptr! block null-block) ; end of freelist
              (disk:write-block! block)))))
   disk)
 
 (define (df disk)
   (define supr (read-super-block disk))
   (blocks-free supr))
 
 (define (new-block disk)
   (define supr (read-super-block disk))
   (define flst (freelist supr))
   (if (null-block? flst)
       (error "disk full! (new-block)" disk)
       (let* ((blck (disk:read-block disk flst)))
         (blocks-free! supr (- (blocks-free supr) 1))
         (freelist!    supr (next-bptr blck))
         (write-super-block! supr)
         blck)))   
 
 (define (delete-block blck)
   (define disk (disk:disk blck))
   (define supr (read-super-block disk))
   (next-bptr! blck (freelist supr))
   (disk:write-block!  blck)
   (freelist!    supr (disk:position blck))
   (blocks-free! supr (+ (blocks-free supr) 1))
   (write-super-block! supr))
 
 (define (delete-chain! disk bptr)
   (unless (null-block? bptr)
     (let* ((blck (disk:read-block disk bptr))
            (next (next-bptr blck)))
       (delete-block blck)
       (delete-chain! disk next))))
 
 (define (cap-name name)
   (if (> (string-length name) filename-size)
       (substring name 0 filename-size)
       name)) ; truncate string to max file name length
 
 (define (maybe-delete-block! blck slot next!)
   (cond 
     ((at-end? blck slot)      ; block appears to be completely empty
      (next! (next-bptr blck)) ;   => delete it
      (delete-block blck))
     ((empty-slot? blck slot)  ; continue checking
      (maybe-delete-block! blck (+ slot 1) next!))))
 
 (define (fresh-block! disk next!)
   (define next (new-block disk))
   (next-bptr! next null-block)
   (do ((slot 0 (+ slot 1)))
     ((at-end? next slot)
      (next! next)
      next)
     (dir-name/ptr! next slot sentinel-filename null-block)))
  
 (define (mk disk name bptr)
   (define supr (read-super-block disk))
   (let loop-dir
     ((dptr (directory supr))
      (new! (lambda (newb)
              (let ((supr (read-super-block disk)))
                (directory! supr (disk:position newb))
                (write-super-block! supr)))))
     (let ((blck (if (null-block? dptr)
                  (fresh-block! disk new!)
                  (disk:read-block disk dptr))))
       (let loop-block
         ((slot 0))
         (cond ((at-end? blck slot)
                (loop-dir (next-bptr blck)
                          (lambda (newb) 
                           (next-bptr! blck (disk:position newb))
                            (disk:write-block! blck))))
               ((empty-slot? blck slot)
                (dir-name/ptr! blck slot name bptr)
                (disk:write-block! blck))
               (else
                (loop-block (+ slot 1))))))))
 
 (define (rm disk name)
   (define supr (read-super-block disk))
   (set! name (cap-name name))
   (let loop-dir
     ((bptr (directory supr))
      (nxt! (lambda (next) 
              (directory! supr next)
              (write-super-block! supr))))
     (let ((blck (if (null-block? bptr)
                     (error "file not found (rm)" name)
                     (disk:read-block disk bptr))))
       (let loop-block
         ((slot 0)
          (seen #f))
         (cond ((at-end? blck slot)
                (loop-dir (next-bptr blck) (lambda (next)
                                             (next-bptr! blck next)
                                             (disk:write-block! blck))))
               ((empty-slot? blck slot)
                (loop-block (+ slot 1) seen))
               ((string=? name (dir-name blck slot))
                (dir-name/ptr! blck slot sentinel-filename null-block)
                (disk:write-block! blck)
                (if (not seen)
                    (maybe-delete-block! blck slot nxt!)))
               (else
                (loop-block (+ slot 1) #t)))))))
  
 (define (ls disk)
   (define supr (read-super-block disk))
   (define bptr (directory supr))
   (if (null-block? bptr)
       ()
       (let traverse-dir
         ((blck (disk:read-block disk bptr))
          (slot 0))
         (cond ((at-end? blck slot)
                (if (has-next? blck)
                    (traverse-dir (disk:read-block disk (next-bptr blck)) 0)
                    ()))
               ((empty-slot? blck slot)
                (traverse-dir blck (+ slot 1)))
               (else
                (cons (cons (dir-name blck slot) (dir-ptr blck slot))
                      (traverse-dir blck (+ slot 1))))))))
 
 (define (whereis disk name)
   (define supr (read-super-block disk))
   (define bptr (directory supr))
   (set!   name (cap-name name))
   (if (null-block? bptr)
       0
       (let traverse-dir 
         ((blck (disk:read-block disk bptr))
          (slot 0))
         (cond ((at-end? blck slot)
                (if (has-next? blck)
                    (traverse-dir (disk:read-block disk (next-bptr blck)) 0)
                    null-block))
               ((string=? name (dir-name blck slot))
                (dir-ptr blck slot))
               (else
                (traverse-dir blck (+ slot 1))))))))